plugins {
    id 'com.android.application'
}

android {
    namespace 'com.ytdlpweb'
    compileSdk 35

    defaultConfig {
        applicationId "com.ytdlpweb"
        minSdk 24
        targetSdk 35
        versionCode Integer.valueOf(project.findProperty('VERSION_CODE') ?: '1')
        versionName project.findProperty('VERSION_NAME') ?: '1.0'
    }

    signingConfigs {
        release {
            def ksFile = file(project.findProperty('RELEASE_STORE_FILE') ?: 'release.keystore')
            if (ksFile.exists()) {
                storeFile ksFile
                storePassword project.findProperty('RELEASE_STORE_PASSWORD') ?: ''
                keyAlias project.findProperty('RELEASE_KEY_ALIAS') ?: ''
                keyPassword project.findProperty('RELEASE_KEY_PASSWORD') ?: ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            def ksFile = file(project.findProperty('RELEASE_STORE_FILE') ?: 'release.keystore')
            signingConfig ksFile.exists() ? signingConfigs.release : signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}
